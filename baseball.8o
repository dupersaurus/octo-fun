:const GROUNDY				23

:const HITX					55

:const STATE_WAIT_PITCH		0
:const STATE_WAIT_SWING		1
:const STATE_BALL			2
:const STATE_SWING 			3
:const STATE_STRIKE			4
:const STATE_HIT			5

:const HIGH_Y				26
:const LOW_Y				30

:alias ballx 		v7
:alias bally		v8
:alias ballxfrac	v9
:alias ballyfrac	va

# velocity is in fractional units/sec (1px = 255)
:alias ballvx  		vb
:alias ballvy  		vc
:alias ballgravity	vd

:alias gamestate v6
:alias watchkey v2

: drawball
	i := ball
	sprite ballx bally 1
;

: drawbatter
	if gamestate < STATE_SWING  then setbatterset
	if gamestate >= STATE_SWING then setbatterswing
;

: drawpitcher
	if gamestate == STATE_WAIT_PITCH then setpitcherset
	if gamestate != STATE_WAIT_PITCH then setpitcherpitch
;
	
: setbatterset
	i := batterset
	v0 := 55
	v1 := GROUNDY
	sprite v0 v1 9
;
	
: setbatterswing
	i := batterhit
	v0 := 55
	v1 := GROUNDY
	sprite v0 v1 9
;

: setpitcherset
	i := pitcherset
	v0 := 2
	v1 := GROUNDY
	sprite v0 v1 9
;

: setpitcherpitch
	i := pitcherthrow
	v0 := 3
	v1 := GROUNDY
	sprite v0 v1 9
;

# jump table for game states
: game-state-lookup
	wait-for-pitch		return
	wait-for-swing		return
;

# wait for the pitcher to select a pitch
: wait-for-pitch
	drawpitcher
	watchkey := key	# blocking

	if watchkey > 9 	then return	# ignore out-of-range key
	if watchkey == 0 	then return

	ballx := 10
	bally := 25

	ballxfrac := 0
	ballyfrac := 0

	# high fastball
	if watchkey == 1 begin			
		ballvx := 255
		ballvy := -20
		ballgravity := 1
	end

	# med fastball
	if watchkey == 4 begin			
		ballvx := 255
		ballvy := -5
		ballgravity := 1
	end

	# low fastball
	if watchkey == 7 begin			
		ballvx := 255
		ballvy := 5
		ballgravity := 1
	end

	# high changeup
	if watchkey == 2 begin			
		ballvx := 200
		ballvy := -70
		ballgravity := 2
	end

	# med changeup
	if watchkey == 5 begin			
		ballvx := 200
		ballvy := -55
		ballgravity := 2
	end

	# low changeup
	if watchkey == 8 begin			
		ballvx := 190
		ballvy := -10
		ballgravity := 1
	end

	# high curve
	if watchkey == 3 begin			
		ballvx := 190
		ballvy := -110
		ballgravity := 3
	end

	# med curve
	if watchkey == 6 begin			
		ballvx := 200
		ballvy := -90
		ballgravity := 3
	end

	# low curve
	if watchkey == 9 begin			
		ballvx := 220
		ballvy := -80
		ballgravity := 3
	end

	drawpitcher
	gamestate := STATE_WAIT_SWING

	drawpitcher
	drawball
;

# ball has been pitched, watching for swing or leave
: wait-for-swing

	# check for swing
	check-swing

	# move ball
	move-ball-pitch
;

# check if the player has swung
: check-swing
	if gamestate != STATE_WAIT_SWING then return

	v2 := HITX

	if v2 <= ballx then v2 =- ballx
	if v2 > ballx then v2 -= ballx

	# swing high
	v0 := 1
	if v0 key begin
		if v2 > 2 begin
			goto-strike
			return
		end

		if bally <= HIGH_Y begin 
			goto-good-hit
			return
		end

		if bally <= LOW_Y begin
			goto-grounder
			return
		end

		goto-strike
	end

	# swing mid
	v0 := 2
	if v0 key begin
		if v2 > 2 begin
			goto-strike
			return
		end

		if bally >= LOW_Y begin
			goto-popup
			return
		end

		if bally <= LOW_Y begin
			goto-grounder
			return
		end

		goto-good-hit
	end
	
	# swing low
	v0 := 3
	if v0 key begin
		if v2 > 2 begin
			goto-strike
			return
		end

		if bally >= LOW_Y begin
			goto-good-hit
			return
		end

		if bally >= HIGH_Y begin 
			goto-popup
			return
		end

		goto-strike
	end
;

: swing 
	drawbatter
	gamestate := STATE_SWING
	drawbatter
;

: goto-strike
	swing

	if gamestate != STATE_STRIKE then drawstrike

	gamestate := STATE_STRIKE
;

: drawstrike
	v0 := 10
	v1 := 4
	i := strikeS
	sprite v0 v1 15

	v0 += 8
	i := strikeT
	sprite v0 v1 15

	v0 += 8
	i := strikeR
	sprite v0 v1 15

	v0 += 8
	i := strikeI
	sprite v0 v1 15

	v0 += 8
	i := strikeK
	sprite v0 v1 15

	v0 += 8
	i := strikeE
	sprite v0 v1 15
;

: goto-good-hit
	swing
	gamestate := STATE_HIT
;

: goto-grounder
	swing
	gamestate := STATE_HIT
;

: goto-popup
	swing
	gamestate := STATE_HIT
;

# step the ball after being pitched
: move-ball-pitch
	if gamestate != STATE_WAIT_SWING then return

	v3 := ballx
	v4 := bally
	v5 := 0

	# move forward
	v1 := ballvx
	ballxfrac += v1
	v3 += vf

	if v3 >= 63 begin
		drawball
		drawpitcher
		gamestate := STATE_WAIT_PITCH
		return
	end

	# apply gravity
	v1 := ballvy
	v1 += ballgravity
	ballvy := v1

	# increment y position
	v1 := ballvy
	
	if ballvy < 128 begin
		ballyfrac += v1
		v4 += vf
	else
		v2 := 255
		v1 =- v2
		ballyfrac += v1
		v4 -= vf
	end

	if v4 >= 32 begin
		drawball
		drawpitcher
		gamestate := STATE_WAIT_PITCH
		return
	end

	if v3 != ballx then if v4 != bally begin
		v5 := 1
	end

	drawball

	ballx := v3
	bally := v4

	drawball
;

: main
	v3 := 0

	drawbatter

	loop
		#v0 := v6
		#v3 := v6
		#jump0 game-state-lookup

		if gamestate == STATE_WAIT_PITCH then	wait-for-pitch
		if gamestate == STATE_WAIT_SWING then	wait-for-swing
	again
;

: ball
	0x80

: batterset
	0x01 0x32 0x14 0x7C 0x38 0x10 0x10 0x28 0x28

: batterhit
	0x00 0x18 0x08 0xF8 0x08 0x08 0x10 0x2C 0x42

: pitcherset
	0x18 0x10 0x38 0x38 0x10 0x10 0x28 0x28 0x28 

: pitcherthrow
	0x00 0x00 0x00 0x14 0x38 0x50 0x10 0x30 0x50

: strikeS 0x00 0x3C 0x7E 0x62 0x60 0x60 0x60 0x3C 0x3E 0x06 0x06 0x46 0x7E 0x3C 0x00 
: strikeT 0x00 0x00 0x3C 0x7E 0x18 0x18 0x18 0x18 0x18 0x18 0x10 0x00 0x00 0x00 0x00 
: strikeR 0x00 0x38 0x6C 0x66 0x62 0x66 0x6C 0x78 0x78 0x6C 0x6C 0x26 0x06 0x00 0x00 
: strikeI 0x00 0x00 0x00 0x7C 0x30 0x30 0x30 0x30 0x30 0x30 0x30 0x30 0x7C 0x7C 0x00 
: strikeK 0x00 0x62 0x6E 0x7C 0x78 0x70 0x60 0x78 0x7C 0x7E 0x66 0x42 0x00 0x00 0x00 
: strikeE 0x00 0x00 0x00 0x3C 0x7C 0x60 0x60 0x78 0x60 0x7E 0x7E 0x7E 0x00 0x00 0x00 